# This script takes two arguments:
#   1. frame number (0-indexed integer)
#   2. number of frames

frontcolor = "white"
backcolor = "black"
profilecolor = "#88ff44"
profile2color = "#ff5555"

set terminal pngcairo background rgb backcolor
call "clock.gpi" ARG1 ARG2
set output sprintf("frames/vela_%02d.png", frame)

unset border
unset tics
unset key
set lmargin at screen 0.01
set rmargin at screen 0.99
set bmargin at screen 0.11
set tmargin at screen 0.99

load "velastats.gpi"

binnow = floor(clock*(binstop - binstart)) + binstart

set xrange [binlo:binhi]
set yrange [I(I_min):I(I_max)]

timespan = (binhi - binlo)*P0/nbins # in seconds
set style textbox 1 opaque fillcolor rgb backcolor noborder margins 5,0
set arrow 1 from screen 0.01, 0.05 to screen 0.99, 0.05 heads back lc rgb frontcolor lw 2
set label 1 sprintf("%.1f ms", timespan*1e3) at screen 0.5, 0.05 font ",16" tc rgb frontcolor center boxed bs 1 front

# And now for the magic color animation!
set palette defined (0 backcolor, 0.9 profilecolor, 1 profile2color)
col(x) = (binnow >= binlo && binnow < binhi ? x : (x < binnow - binwidth ? 0 : (x - (binnow - binwidth))/binwidth))

set pm3d map
splot infile u 3:(I($4)):(col($3)) every ::binstart::binnow with lines lw 2 lc palette z
